(* 
                         CS 51 Final Project
                         MiniML -- Evaluation
*)

(* This module implements a small untyped ML-like language under
   various operational semantics.
 *)

open Expr ;;
  
(* Exception for evaluator runtime, generated by a runtime error in
   the interpreter *)
exception EvalError of string ;;
  
(* Exception for evaluator runtime, generated by an explicit `raise`
   construct in the object language *)
exception EvalException ;;

(*......................................................................
  Environments and values 
 *)

module type ENV = sig
    (* the type of environments *)
    type env
	   
    (* the type of values (including closures) stored in
       environments *)
    type value =
      | Val of expr
      | Closure of (expr * env)
   
    (* empty () -- Returns an empty environment *)
    val empty : unit -> env

    (* close expr env -- Returns a closure for `expr` and its `env` *)
    val close : expr -> env -> value

    (* lookup env varid -- Returns the value in the `env` for the
       `varid`, raising an `Eval_error` if not found *)
    val lookup : env -> varid -> value

    (* extend env varid loc -- Returns a new environment just like
       `env` except that it maps the variable `varid` to the `value`
       stored at `loc`. This allows later changing the value, an
       ability used in the evaluation of `letrec`. To make good on
       this, extending an environment needs to preserve the previous
       bindings in a physical, not just structural, way. *)
    val extend : env -> varid -> value ref -> env

    (* env_to_string env -- Returns a printable string representation
       of environment `env` *)
    val env_to_string : env -> string
                                 
    (* value_to_string ?printenvp value -- Returns a printable string
       representation of a value; the optional flag `printenvp`
       (default: `true`) determines whether to include the environment
       in the string representation when called on a closure *)
    val value_to_string : ?printenvp:bool -> value -> string
  end

module Env : ENV =
  struct
    type env = (varid * value ref) list
     and value =
       | Val of expr
       | Closure of (expr * env)

    let empty () : env = []
    
    let close (exp : expr) (env : env) : value =
      Closure (exp, env)

    let lookup (env : env) (varname : varid) : value =
      match List.assoc_opt varname env with
      | None -> raise (EvalError "Var undefined in env")
      | Some v -> !v

    let extend (env : env) (varname : varid) (loc : value ref) : env =
        (varname, loc) :: (List.remove_assoc varname env)

    let rec value_to_string ?(printenvp : bool = true) (v : value) : string =
      match v with
      | Val (x) -> exp_to_concrete_string x
      | Closure (exp, env) -> 
          "Closure (Val = " ^ exp_to_concrete_string exp ^ 
          ", Env : " ^ (if printenvp then env_to_string env ^ ")" else "{})")
      and env_to_string (env : env) : string =
      "{" ^ (List.fold_left (fun acc e -> let var, value = e in 
                            "" ^ var ^ " â†¦ " ^ value_to_string !value ^ ";" ^ acc) "" env) ^ "}"
  end
;;


(*......................................................................
  Evaluation functions

  Each of the evaluation functions below evaluates an expression `exp`
  in an environment `env` returning a result of type `value`. We've
  provided an initial implementation for a trivial evaluator, which
  just converts the expression unchanged to a `value` and returns it,
  along with "stub code" for three more evaluators: a substitution
  model evaluator and dynamic and lexical environment model versions.

  Each evaluator is of type `expr -> Env.env -> Env.value` for
  consistency, though some of the evaluators don't need an
  environment, and some will only return values that are "bare
  values" (that is, not closures). 

  DO NOT CHANGE THE TYPE SIGNATURES OF THESE FUNCTIONS. Compilation
  against our unit tests relies on their having these signatures. If
  you want to implement an extension whose evaluator has a different
  signature, implement it as `eval_e` below.  *)

(* The TRIVIAL EVALUATOR, which leaves the expression to be evaluated
   essentially unchanged, just converted to a value for consistency
   with the signature of the evaluators. *)

(* Helper functions *)

let binop_match (b : binop) (e1 : expr) (e2 : expr) : expr =
  match b with
  | Plus -> 
    (match e1, e2  with
    | Num x, Num y -> Num (x + y)
    | _ -> raise (EvalError "Expected Num"))
  | Minus -> 
    (match e1, e2  with
    | Num x, Num y -> Num (x - y)
    | _ -> raise (EvalError "Expected Num"))
  | FloatPlus -> 
    (match e1, e2  with
    | Float x, Float y -> Float (x +. y)
    | _ -> raise (EvalError "Expected Float"))
  | FloatMinus ->
    (match e1, e2  with
    | Float x, Float y -> Float (x -. y)
    | _ -> raise (EvalError "Expected Float"))
  | Times ->
    (match e1, e2  with
    | Num x, Num y -> Num (x * y)
    | _ -> raise (EvalError "Expected Num"))
  | FloatTimes -> 
    (match e1, e2  with
    | Float x, Float y -> Float (x *. y)
    | _ -> raise (EvalError "Expected Float"))
  | Equals ->
    (match e1, e2  with
    | Float x, Float y -> Bool (x = y)
    | Num x, Num y -> Bool (x = y)
    | Bool x, Bool y -> Bool (x = y)
    | _ -> raise (EvalError "Invalid Operation"))
  | GreaterThan ->
    (match e1, e2  with
    | Float x, Float y -> Bool (x > y)
    | Num x, Num y -> Bool (x > y)
    | Bool x, Bool y -> Bool (x > y)
    | _ -> raise (EvalError "Invalid Operation"))
  | LessThan ->
    (match e1, e2  with
    | Float x, Float y -> Bool (x < y)
    | Num x, Num y -> Bool (x < y)
    | Bool x, Bool y -> Bool (x < y)
    | _ -> raise (EvalError "Invalid Operation"))
  | Concat -> 
    (match e1, e2 with
     | String s1, String s2 -> String (s1 ^ s2)
     | _ -> raise (EvalError "Expected String"))


(*Retrieves expression from a value - utilized for susbtitution*)

let val_to_exp (v : Env.value) : expr =
  match v with
  | Env.Val (arg) -> arg
  | _ -> raise EvalException

let eval_t (exp : expr) (_env : Env.env) : Env.value =
  (* coerce the expr, unchanged, into a value *)
  Env.Val exp ;;

(* The SUBSTITUTION MODEL evaluator -- to be completed *)
   
let rec eval_s (exp : expr) (env : Env.env) : Env.value =
  match exp with
  | Var v -> raise (EvalError ("Unbound variable: " ^ v))
  | Num n -> Env.Val (Num n)
  | Float x -> Env.Val (Float x)
  | Bool b -> Env.Val (Bool b)
  | String s -> Env.Val (String s)
  | Unop (_, e) -> 
    let result = eval_s e env in 
    (match result with
    | Env.Val (Num n) -> Env.Val (Num ~-n)
    | _ -> raise (EvalError "Invalid Operation"))
  | Binop (b, e1, e2) ->
    let result1 = eval_s e1 env in 
    let result2 = eval_s e2 env in 
    Env.Val (binop_match b (val_to_exp result1) (val_to_exp result2))
  | Fun (v, e) -> Env.Val (Fun (v, e))
  | Let (v, e1, e2) -> let x = val_to_exp (eval_s e1 env) in eval_s (subst v x e2) env
  | Letrec (x, def, body) ->
    (match eval_s def env with
     | Env.Val v ->
       eval_s (subst x (subst x (Letrec (x, v, Var x)) v) body) env
     | _ -> raise (EvalError "type error"))
  | Conditional (cond, e1, e2) -> 
        (match eval_s e1 env with 
        | Val (Bool true) -> eval_s e1 env
        | Val (Bool false) -> eval_s e2 env
        | _ -> raise (EvalError "Expected Bool"))
  | App (f, e) ->
    (match eval_s f env, eval_s e env with
      | Env.Val Fun (v, b), Env.Val x -> eval_s (subst v x b) env
      | _ -> raise (EvalError "Invalid Function"))
  | Raise -> raise EvalException 
  | Unassigned -> raise (EvalError "Unassigned");;


(* The DYNAMICALLY-SCOPED ENVIRONMENT MODEL evaluator -- to be
   completed *)
   
let rec eval_d (exp : expr) (env : Env.env) : Env.value =
  match exp with
  | Var v -> Env.lookup env v
  | Num n -> Env.Val (Num n)
  | Float x -> Env.Val (Float x)
  | Bool b -> Env.Val (Bool b)
  | String s -> Env.Val (String s)
  | Unop (_, e) -> 
    let result = eval_d e env in 
    (match result with
    | Env.Val (Num n) -> Env.Val (Num ~-n)
    | _ -> raise (EvalError "Invalid Operation"))
  | Binop (b, e1, e2) ->
    let result1 = eval_d e1 env in 
    let result2 = eval_d e2 env in 
    Env.Val (binop_match b (val_to_exp result1) (val_to_exp result2))
  | Fun (v, e) -> Env.Val (Fun (v, e))
  | Let (v, e1, e2) -> eval_d e2 (Env.extend env v (ref (eval_d e1 env)))
  | Letrec (v, e1, e2) -> 
    let v1 = eval_d e1 env in 
    let v2 = eval_d e2 (Env.extend env v (ref v1)) in v2
  | Conditional (cond, e1, e2) -> 
        (match eval_d cond env with 
        | Val (Bool true) -> eval_d e1 env
        | Val (Bool false) -> eval_d e2 env
        | _ -> raise (EvalError "Expected Bool"))
  | App (f, e) ->
    (match eval_d f env, eval_d e env with
     | Env.Val Fun (v, b), x -> eval_d b (Env.extend env v (ref x))
     | _ -> raise (EvalError "Invalid Function"))
  | Raise -> raise EvalException 
  | Unassigned -> raise (EvalError "Unassigned");;


       
(* The LEXICALLY-SCOPED ENVIRONMENT MODEL evaluator -- optionally
   completed as (part of) your extension *)
   
let eval_l (_exp : expr) (_env : Env.env) : Env.value =
  failwith "eval_l not implemented" ;;

(* The EXTENDED evaluator -- if you want, you can provide your
   extension as a separate evaluator, or if it is type- and
   correctness-compatible with one of the above, you can incorporate
   your extensions within `eval_s`, `eval_d`, or `eval_l`. *)

let eval_e _ =
  failwith "eval_e not implemented" ;;
  
(* Connecting the evaluators to the external world. The REPL in
   `miniml.ml` uses a call to the single function `evaluate` defined
   here. Initially, `evaluate` is the trivial evaluator `eval_t`. But
   you can define it to use any of the other evaluators as you proceed
   to implement them. (We will directly unit test the four evaluators
   above, not the `evaluate` function, so it doesn't matter how it's
   set when you submit your solution.) *)
   
let evaluate = eval_t ;;
